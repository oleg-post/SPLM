{
    "version":{
        "_CMT_":"Возвращает информацию о версии требования. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select distinct ?version ?file_lbl ?itemver_lbl ?loc_lbl ?val_loc ?val_seq ?item ?item_lbl ?item_cls ?bs ?pbs ?cr1 ?cr1_lbl ?cr1_cls ?cr2 ?cr2_lbl ?cr2_cls ?criterion {
                    BIND (<#{URI}> as ?itemver) .
                    ?item onto:hasVersion ?itemver .
                    ?item rdfs:label ?item_lbl .
                    ?item a ?item_cls .
                    Optional{
                        ?itemver onto:hasAttribute ?att_seq .
                        ?att_seq a onto:SequenceNumber .
                        ?att_seq onto:hasAttributeValue ?attval_seq .
                        ?attval_seq onto:value ?val_seq .
                    } .
                    Optional {?itemver rdfs:label ?itemver_lbl . } .
                    ?itemver onto:value ?version .
                    OPTIONAL {
                        ?itemver onto:isFromFile ?file .
                        ?file rdfs:label ?file_lbl .
                    }
                    OPTIONAL {
                        ?cr1 onto:hasOldReqVersion ?itemver .
                        ?cr1 rdf:type ?cr1_cls .
                        ?cr1 rdfs:label ?cr1_lbl .
                    }
                    OPTIONAL {
                        ?cr2 onto:hasNewReqVersion ?itemver .
                        ?cr2 rdf:type ?cr2_cls .
                        ?cr2 rdfs:label ?cr2_lbl .
                    }
                    OPTIONAL {
                        ?itemver onto:hasCriterion ?crio .
                        ?crio onto:value ?criterion .
                    }
                } order by DESC(?val_loc)"
    },
    "info":{
        "_CMT_":"Возвращает информацию о требовании. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select ?version ?itemver ?itemver_lbl ?doc ?doc_lbl ?docname_lbl ?item_lbl ?typ_lbl ?comp {
                    BIND (<#{URI}> as ?item) .
                    ?item a ?typ .
                    ?typ rdfs:label ?typ_lbl .
                    ?item onto:hasVersion ?itemver .
                    ?item onto:isCreatedIn ?doc .
                    ?doc rdfs:label ?doc_lbl .
                     Optional {
                        {select ?item (group_concat(?cnc; separator = \"; \") as ?comp) where {
                                ?item onto:hasReqImplementer ?company .
                                ?company rdfs:label ?company_lbl .
                                BIND (CONCAT(STR(?company), \", \", ?company_lbl) as ?cnc ).
                            } group by ?item
                            }
                    } .
                    ?doc onto:hasAttribute ?att .
                    ?att a onto:DocumentName .
                    ?att onto:hasAttributeValue ?val .
                    ?val rdfs:label ?docname_lbl .

                    optional { ?itemver rdfs:label ?itemver_lbl . } .
                    ?item rdfs:label ?item_lbl .
                    ?itemver onto:value ?version .
                    ?itemver onto:latestVersion xs:true .
                 } order by DESC(?version)"
    },
    "all_vers":{
        "_CMT_":"Возвращает список всех версий требования, отсортированных в порядке убывания. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT ?itemver ?itemver_lbl ?itemver_cls{
                    BIND (<#{URI}> as ?item) .
                    ?item onto:hasVersion ?itemver .
                    ?itemver rdf:type ?itemver_cls .
                    OPTIONAL{ ?itemver onto:latestVersion ?flag } . FILTER (!BOUND(?flag))
                    ?itemver onto:value ?itemver_lbl .
                } ORDER BY DESC(?itemver_lbl)"
    },
    "pq1":{
        "_CMT_":"Возвращает список документов, связанных с версией требования. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select ?docv ?docv_cls ?docv_lbl ?ver ?val_lbl ?company ?company_lbl ?company_cls {
                    BIND (<#{URI}> as ?itemver) .
                    ?itemver onto:isTracedTo ?docv .
                    ?itemver onto:latestVersion xs:true .
                    OPTIONAL {
                    ?docv rdfs:label ?ver .
                    ?docv rdf:type ?docv_cls .
                    ?docv onto:latestVersion xs:true .
                    }
                    ?doc onto:hasVersion ?docv .
                    ?doc rdfs:label ?docv_lbl .
                    ?doc onto:hasAttribute ?att .
                    ?att a onto:DocumentName .
                    ?att onto:hasAttributeValue ?val .
                    ?val rdfs:label ?val_lbl . 
                    Optional {
                        ?doc onto:madeBy ?company .
                        ?company rdfs:label ?company_lbl . 
                        ?company a ?company_cls
                    } .
                }"
    },
    "cnt_docs":{
        "_CMT_":"Возвращает количество связанных документов. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select  (Count(?docver) as ?cnt) {
                    BIND (<#{URI}> as ?item) .
                    ?item onto:hasVersion ?itemver .
                    ?itemver onto:value ?version .
                    ?itemver onto:latestVersion xs:true .
                    
                    ?itemver onto:isTracedTo ?docver .
                    ?docver onto:latestVersion xs:true .
                } group by ?itemver"
    },
    "ver_verify_result":{
        "_CMT_":"Возвращает количество связанных документов. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select ?status ?comment ?prsn_lbl ?date ?docver_cls (group_concat(?traced_doc; separator=\"; \") as ?doc_list) {
                    <#{URI}> onto:hasRecordOfVerification ?record .
                    ?record onto:hasAttribute ?att_v .
                    ?att_v a onto:Verification .
                    ?att_v onto:hasAttributeValue/rdfs:label ?status .
                    ?record onto:isVerifiedBy ?prsn .
                    ?prsn rdfs:label ?prsn_lbl .
                    ?report onto:hasRecord ?record .
                    ?report onto:hasAttribute ?att_recdate .
                    ?att_recdate a onto:DateOfDocumentRelease .
                    ?att_recdate onto:hasAttributeValue/onto:value ?date .
                    Optional {
                        ?record onto:hasAttribute ?cmm_att .
                        ?cmm_att a onto:ReqExplanatoryNote .
                        ?cmm_att onto:hasAttributeValue ?cmm_val .
                        ?cmm_val rdfs:label ?comment .
                    } .
                    ?record onto:hasSection ?sec .
                    ?sec rdfs:label ?sec_lbl .
                    ?docver onto:hasSectionOfVerification ?sec .
                    ?docver a ?docver_cls .
                    ?doc onto:hasVersion ?docver .
                    ?doc rdfs:label ?doc_lbl .
                    BIND (CONCAT(str(?docver), \", \", ?doc_lbl) as ?traced_doc ).
                } group by ?status ?comment ?prsn_lbl ?date ?docver_cls"
    },
    "yvl_attrs":{
        "_CMT_":"Возвращает дополнительные аттрибуты версии YVL требования. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT ?attCls ?attClsLbl (group_concat(?attValue; separator = \"; \") as ?cnc) {
                    BIND (<#{URI}> as ?itemver) .
                    ?itemver onto:hasAttribute ?attr .
                    ?attr rdf:type ?attCls . FILTER (?attCls != onto:SequenceNumber) .
                    ?attr onto:hasAttributeValue ?attr_val .
                    ?attr_val onto:value ?attValue .
                    ?attCls rdfs:label ?attClsLbl .
                } GROUP BY ?attCls ?attClsLbl ORDER BY ?attClsLbl"
    },
    "contr_attrs":{
        "_CMT_":"Возвращает дополнительные аттрибуты версии Contract требования. Страница /requirements/item|version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT ?itemver ?attCls ?attClsLbl ?attValue {
                    BIND (<#{URI}> as ?itemver) .
                    ?itemver onto:hasAttribute ?attr .
                    ?attr rdf:type ?attCls . FILTER (?attCls != onto:SequenceNumber && ?attCls != onto:DBS) .
                    ?attr onto:hasAttributeValue ?attr_val .
                    ?attr_val rdfs:label ?attValue .
                    ?attCls rdfs:label ?attClsLbl . 
                } ORDER BY ?attClsLbl"
    },
    "criterion_list":{
        "_CMT_":"Возвращает список кретериев для проверки требований",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"Select ?criter_lbl ?criter_date ?company ?ver ?ver_lbl ?ver_cls ?val ?text {
                    ?item a onto:Requirement .
                    ?item rdfs:label ?ver_lbl .
                    ?item onto:hasVersion ?ver .
                    ?ver onto:value ?val .
                    ?ver onto:latestVersion xs:true .
                    ?ver a ?ver_cls .
                    ?ver onto:hasCriterion ?criter .
                    ?criter rdfs:label ?criter_lbl .
                    ?criter onto:value ?text .
                    ?criter onto:madeBy/rdfs:label ?company .
                    ?criter onto:hasAttribute ?dt .
                    ?dt a onto:DateOfDocumentRelease .
                    ?dt onto:hasAttributeValue/onto:value ?criter_date .
                } order by ?ver_lbl"
    },
    "parents":{
        "_CMT_":"Возвращает родительскую версию требований",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT ?doc ?doc_cls ?doc_lbl ?req ?req_cls ?req_lbl ?ver ?txt ?req_uri{
                    <#{URI}> onto:hasParentReq ?req .
                    ?req rdf:type ?req_cls .
                    ?req rdfs:label ?req_lbl .
                    ?req onto:hasVersion ?reqver .
                    ?reqver rdfs:label ?txt .
                    ?reqver onto:value ?ver .
                    ?req onto:isCreatedIn ?doc .
                    ?doc rdfs:label ?doc_lbl .
                    ?doc rdf:type ?doc_cls .
                    BIND(STR(?req) AS ?req_uri) .
                } ORDER BY DESC(?req_lbl)"
    },
    "childs":{
        "_CMT_":"Возвращает возвращает декомпозицию",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT ?doc ?doc_cls ?doc_lbl ?req ?req_cls ?req_lbl ?ver ?txt ?req_uri{
                    ?req onto:hasParentReq <#{URI}> .
                    ?req rdf:type ?req_cls .
                    ?req rdfs:label ?req_lbl .
                    ?req onto:hasVersion ?reqver .
                    ?reqver rdfs:label ?txt .
                    ?reqver onto:value ?ver .
                    ?req onto:isCreatedIn ?doc .
                    ?doc rdfs:label ?doc_lbl .
                    ?doc rdf:type ?doc_cls .
                    BIND(STR(?req) AS ?req_uri) .
                } ORDER BY DESC(?req_lbl)"
    }
}
