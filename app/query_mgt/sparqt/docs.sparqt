{
    "info":{
        "_CMT_":"Информация о проектном документе. Страница /doc.php",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT ?doc ?doc_lbl ?class_lbl ?name ?company ?company_cls ?company_lbl ?class_c_lbl ?comm ?docver ?version ?link_lbl1 {
                    BIND(<#{URI}> as ?doc ) .
                    ?doc rdf:type ?prdoc_cls .
                    ?doc rdfs:label ?doc_lbl .
                    ?doc onto:hasAttribute ?att .
                    ?att a onto:DocumentName .
                    ?att a ?att_class .
                    ?att_class rdfs:label ?class_lbl .
                    ?att onto:hasAttributeValue ?attval .
                    ?attval rdfs:label ?name .
                    OPTIONAL {
                        ?doc onto:madeBy ?company .
                        ?company rdf:type ?company_cls .
                        ?company rdfs:label ?company_lbl .
                    }
                    OPTIONAL {
                        ?doc onto:hasAttribute ?att_c .
                        ?att_c a onto:Comment .
                        ?att_c a ?att_c_class .
                        ?att_c_class rdfs:label ?class_c_lbl .
                        ?att_c onto:hasAttributeValue ?attval_c .
                        ?attval_c rdfs:label ?comm .
                    }
                    OPTIONAL {
                        ?doc onto:hasVersion ?docver .
                        ?docver onto:latestVersion xs:true .
                        ?docver rdfs:label ?version .
                    }
                    OPTIONAL {
                        ?doc onto:hasAttribute ?link .
                        ?link a onto:StringURL .
                        ?link onto:hasAttributeValue ?linkval .
                        ?linkval rdfs:label ?link_lbl1 .
                    }
                }"
    },
    "vers_list":{
        "_CMT_":"Список версий документа. Страница /documents[/doc_type]/item | /documents[/doc_type]/version",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"SELECT DISTINCT ?docver ?docver_cls ?docver_lbl{
                    BIND(<#{URI}> as ?doc ) .
                    ?doc onto:hasVersion ?docver .
                    OPTIONAL { ?docver onto:latestVersion ?flag } . FILTER (!BOUND(?flag))
                    ?docver a ?docver_cls .
                    ?docver rdfs:label ?docver_lbl .
                } ORDER BY DESC(?docver_lbl)"
    },
    "info_ver":{
        "_CMT_":"Информация о версии проектного документа. Страница /docver.php",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select distinct ?prdocver ?prdocver_cls ?prdoc ?prdoc_cls ?prdoc_lbl ?vr ?val ?data_issue ?company ?company_cls ?company_lbl ?link_lbl1 ?verify_doc_link {
                    BIND (<#{URI}> as ?prdocver).
                    ?prdocver rdf:type ?prdocver_cls .
                    ?prdocver rdfs:label ?vr .
                    ?attval rdfs:label ?val .
                    OPTIONAL{
                        ?prdocver onto:hasRecordOfVerification ?iddocverfrec .
                        ?iddocverfrec onto:hasAttribute ?att_vrfrecconcl .
                        ?att_vrfrecconcl a onto:Verification .
                        ?att_vrfrecconcl onto:hasAttributeValue/rdfs:label ?docconclval .
                        ?iddocverfrec onto:isVerifiedBy/rdfs:label ?exp_lbl .
                        ?iddocverfrec onto:hasAttribute ?comment .
                        ?comment a onto:DocExplanatoryNote .
                        ?comment onto:hasAttributeValue/rdfs:label ?comment_val .

                        ?verify_doc onto:hasRecord ?iddocverfrec .
                        ?verify_doc onto:hasAttribute ?att_doc_link .
                        ?att_doc_link a onto:StringURL .
                        ?att_doc_link onto:hasAttributeValue/rdfs:label ?verify_doc_link .
                    }
                    ?prdoc onto:hasVersion ?prdocver .
                    ?prdoc rdfs:label ?prdoc_lbl .
                    ?prdoc rdf:type ?prdoc_cls .
                    ?prdoc onto:hasAttribute ?att .
                    ?att a onto:DocumentName .
                    ?att onto:hasAttributeValue ?attval .
                    OPTIONAL{
                        ?prdocver onto:hasAttribute ?att_date .
                        ?att_date onto:hasAttributeValue ?attval_date .
                        ?attval_date onto:value ?data_issue .
                    }
                    ?prdoc onto:madeBy ?company .
                    ?company rdf:type ?company_cls .
                    ?company rdfs:label ?company_lbl .
                    OPTIONAL {
                        ?prdoc onto:hasAttribute ?link .
                        ?link a onto:StringURL .
                        ?link onto:hasAttributeValue ?linkval .
                        ?linkval rdfs:label ?link_lbl1 .
                    }
                }"
    },
    "tbl1":{
        "_CMT_":"Список, учтенных в версии проектного документа, требований. Страница /docver.php",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select distinct ?doc_lbl ?ver_lbl ?ver ?ver_cls ?ver_val ?vertxt  {
                    ?ver onto:isTracedTo <#{URI}> .
                    ?ver a ?ver_cls .
                    ?ver onto:value ?ver_val .
                    ?ver rdfs:label ?vertxt .
                    ?ver onto:isFromDoc/rdfs:label ?doc_lbl .
                    ?item onto:hasVersion ?ver .
                    ?item a onto:Requirement .
                    ?item rdfs:label ?ver_lbl .
                } order by ?doc_lbl ?ver_lbl"
    },
    "ver_state":{
        "_CMT_":"Возвращает текущую версию документа. Страница /docver.php",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select ?docver ?r_lbl ?pdf{
                    <#{URI}> onto:hasRecordOfVerification ?rec .
                    ?rec onto:hasAttribute ?r_att .
                    ?rec onto:hasAttachment ?pdf .
                    ?r_att a onto:Verification .
                    ?r_att onto:hasAttributeValue ?r_val .
                    ?r_val rdfs:label ?r_lbl .
                 }"
    },
    "ver_verify_reqs":{
        "_CMT_":"Список проверенных требований связанных с версией документа. Страница /docver.php",
        "#VARS":{"URI":{"mark":"#{URI}","default":""}},
        "#TXT":"select ?reqver ?reqver_cls ?reqver_lbl ?req_text ?version ?ver_val ?comment {
                    <#{URI}> onto:hasRecordOfVerification ?rec .
                    ?rec onto:hasAttribute ?r_att .
                    ?r_att a onto:Verification .
                    ?r_att onto:hasAttributeValue ?r_val .
                    ?r_val rdfs:label ?r_lbl .
                    ?rec onto:hasSection ?sec .
                    ?reqver onto:hasSectionOfVerification ?sec .
                    ?reqver a ?reqver_cls .
                    ?req a onto:Requirement .
                    ?req onto:hasVersion ?reqver .
                    ?req rdfs:label ?reqver_lbl .
                    OPTIONAL{
                        ?reqver rdfs:label ?req_text .
                    }
                    ?reqver onto:value ?version .
                    ?sec onto:hasAttribute ?s_att .
                    ?s_att a onto:Verification .
                    ?s_att onto:hasAttributeValue ?s_val .
                    ?s_val rdfs:label ?ver_val .
                    Optional {
                        ?sec onto:hasAttribute ?cmm_att .
                        ?cmm_att a onto:ReqDocExplanatoryNote .
                        ?cmm_att onto:hasAttributeValue ?cmm_val .
                        ?cmm_val rdfs:label ?comment .
                    } .
                }"
    }
}
